nextflow_workflow {

    name "Test Subworkflow QC_WORKFLOW"
    script "../main.nf"
    config "./nextflow.config"
    workflow "QC_WORKFLOW"
    tag "qc"
    tag "subworkflow"

    test("QC_WORKFLOW - Homo sapiens chr22") {

        // Generate refflat file
        setup {

            // Create refflat reference
            run("GTF_TO_REFFLAT") {
                script "../../../../modules/local/uscs/custom_gtftogenepred/main.nf"
                process {
                    """
                    input[0] =Channel.of(
                        [
                            [id: 'gtf'],
                            file("https://raw.githubusercontent.com/nf-core/test-datasets/refs/heads/modules/data/genomics/homo_sapiens/genome/genome.gtf", checkIfExists: true)
                        ]
                    )
                    """
                }
            }

            // Filter GTF to extract rRNA genes
            run("GET_RRNA_TRANSCRIPTS") {
                script "../../../../modules/local/get_rrna_transcript/main.nf"
                process {
                    """
                    input[0] = Channel.of(
                        [
                            [id: 'fasta'],
                            file("https://raw.githubusercontent.com/nf-core/test-datasets/refs/heads/modules/data/genomics/homo_sapiens/genome/genome.gtf", checkIfExist: true)
                        ]
                    )
                    """
                }
            }


            // Convert rRNA BED to interval list (the necessary file)
            run("GATK4_BEDTOINTERVALLIST") {
                script "../../../../modules/nf-core/gatk4/bedtointervallist/main.nf"
                process {
                    """
                    input[0] = GET_RRNA_TRANSCRIPTS.out.bed
                    input[1] = Channel.of(
                        [
                            [id: 'chr22_dic'],
                            file("https://raw.githubusercontent.com/nf-core/test-datasets/refs/heads/modules/data/genomics/homo_sapiens/genome/genome.dict", checkIfExist: true)
                        ]
                    )
                    """
                }
            }

        }

        when {
            // Params to activate modules ext.when condition
            params {
                skip_qc              = false
                fusioninspector_only = false
                starfusion           = true
                all                  = true
            }

            workflow {
                """
                // ch_bam_sorted
                input[0] = Channel.of(
                    [
                        [id: "chr22_bam"],
                        file("https://raw.githubusercontent.com/nf-core/test-datasets/refs/heads/modules/data/genomics/homo_sapiens/illumina/bam/test.paired_end.sorted.bam", checkIfExists: true)
                    ])

                // ch_refflat
                input[1] = GTF_TO_REFFLAT.out.refflat.map { that -> [[id:that.Name], that] }
                // ch_fasta
                input[2] = Channel.of(
                    [
                        [ id: "test_ref" ],
                        file("https://raw.githubusercontent.com/nf-core/test-datasets/refs/heads/modules/data/genomics/homo_sapiens/genome/genome.fasta", checkIfExist: true)
                    ]
                )

                // ch_fai
                input[3] = Channel.of(
                    [
                        [ id: "test_ref" ],
                        file("https://raw.githubusercontent.com/nf-core/test-datasets/refs/heads/modules/data/genomics/homo_sapiens/genome/genome.fasta.fai", checkIfExist: true)
                    ] )

                // ch rRNA interval list
                input[4] = GATK4_BEDTOINTERVALLIST.out.interval_list
                """
            }
        }

        then {
            assertAll(
                { assert workflow.success },
                { assert snapshot(
					file(workflow.out.rnaseq_metrics[0][1]).readLines()[5..8],
					file(workflow.out.duplicate_metrics[0][1]).readLines()[5..8],
					file(workflow.out.insertsize_metrics[0][1]).readLines()[5..8],
                    workflow.out.versions
                ).match() }
            )
        }
    }

}
